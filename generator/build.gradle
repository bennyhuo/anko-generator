apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = "org.jetbrains.android.anko.Main"

run {
    // Replace "verify" with "generate" to update generated files
    def generatorMode = findProperty("anko.generator.mode") ?: System.getenv("ANKO_GENERATOR_MODE") ?: "verify"
    logger.debug("Anko generator mode: {}", generatorMode)

    args = ["versions",
            generatorMode,
            "--configuration=anko/props/configuration.json",
            "--outputDirectory=anko/library/generated",
            "--androidSdk=dependencies/android-sdk"]

    workingDir = rootDir
}

sourceSets {
    main {
        java.srcDirs = ['src']
        kotlin.srcDirs = ['src']
    }
    test {
        java.srcDirs = ['test']
        kotlin.srcDirs = ['test']
    }
}

dependencies {
    api('com.github.javaparser:javaparser-core:3.0.1')
    api('org.jtwig:jtwig-core:5.65')
    api('org.jtwig:jtwig-spaceless-extension:1.45')
    api('com.samskivert:jmustache:1.10')
    api('com.google.code.gson:gson:2.8.0')
    api('org.slf4j:slf4j-simple:1.7.22')
    api('org.ow2.asm:asm-debug-all:5.1')
    api("org.jetbrains.kotlinx:kotlinx.dom:0.0.10")
    api("org.jetbrains.kotlin:kotlin-stdlib")
}